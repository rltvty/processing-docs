<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
	<head>
		<title>Spore2 \ Examples \ Processing.org</title>
		
		<link rel="icon" href="/favicon.ico" type="image/x-icon" />
		
		<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
		<meta name="Author" content="Processing Foundation" />
		<meta name="Publisher" content="Processing Foundation" />
		<meta name="Keywords" content="Processing, Sketchbook, Programming, Coding, Code, Art, Design" />
		<meta name="Description" content="Processing is a flexible software sketchbook and a language for learning how to code within the context of the visual arts. Since 2001, Processing has promoted software literacy within the visual arts and visual literacy within technology." />
		<meta name="Copyright" content="All contents copyright the Processing Foundation, Ben Fry, Casey Reas, and the MIT Media Laboratory" />
	    
	    <script src="../javascript/modernizr-2.6.2.touch.js" type="text/javascript"></script>
		<link href="../css/style.css" rel="stylesheet" type="text/css" />
	</head>
	<body id="Examples" onload="" >
		
		<!-- ==================================== PAGE ============================ --> 
		<div id="container">
	
			<!-- ==================================== HEADER ============================ --> 
			<div id="ribbon">
				<ul class="left">
					<li class="highlight"><a href="http://processing.org/">Processing</a></li>
					<li><a href="http://p5js.org/">p5.js</a></li>
					<li><a href="http://py.processing.org/">Processing.py</a></li>
					<li><a href="http://android.processing.org/">Processing for Android</a></li>
				</ul>
				<ul class="right">
					<li><a href="https://processingfoundation.org/">Processing Foundation</a></li>
				</ul>
				<div class="clear"></div>
			</div>
			<div id="header">
				<a href="/" title="Back to the Processing cover."><div class="processing-logo no-cover" alt="Processing cover"></div></a>
				<form name="search" action="//www.google.com/search" method="get">
				<!--<label>Search processing.org</label>-->
				       <p><input type="hidden" name="as_sitesearch" value="processing.org" />
				       <input type="text" name="as_q" value="" size="20" class="text" /> 
						<input type="submit" value=" " /></p>
				</form>
			</div> 
			<a id="TOP" name="TOP"></a>
			
			<!-- ==================================== NAVIGATION ============================ -->
						<div id="navigation">
				<div class="navBar" id="mainnav_noSub">
					<a href="/">Cover</a><br><br>
					<a href="/download/">Download</a><br>
					<a href="/download/support.html">Donate</a><br><br>
					<a href="/exhibition/">Exhibition</a><br><br>
					<a href="/reference/">Reference</a><br>
					<a href="/reference/libraries/">Libraries</a><br>
					<a href="/reference/tools/">Tools</a><br>
					<a href="/reference/environment/">Environment</a><br><br>
					<a href="/tutorials/">Tutorials</a><br>
					<a href="/examples/" class="active">Examples</a><br>
					<a href="/books/">Books</a><br>
					<a href="/handbook/">Handbook</a><br><br />
					<a href="/overview/">Overview</a><br> 
					<a href="/people/">People</a><br><br>
					<a href="/shop/">Shop</a><br><br>
					<a href="http://forum.processing.org"class="outward"><span>&raquo;</span>Forum</a><br> 
					<a href="https://github.com/processing"class="outward"><span>&raquo;</span>GitHub</a><br> 
					<a href="https://github.com/processing/processing/issues?state=open"class="outward"><span>&raquo;</span>Issues</a><br> 
					<a href="https://github.com/processing/processing/wiki/"class="outward"><span>&raquo;</span>Wiki</a><br> 
					<a href="https://github.com/processing/processing/wiki/FAQ"class="outward"><span>&raquo;</span>FAQ</a><br> 
					<a href="https://twitter.com/processingOrg"class="outward"><span>&raquo;</span>Twitter</a><br> 
					<a href="https://www.facebook.com/page.processing"class="outward"><span>&raquo;</span>Facebook</a><br> 
					<a href="https://medium.com/@ProcessingOrg"class="outward"><span>&raquo;</span>Medium</a><br> 
				</div>
			</div>


			<!-- ==================================== CONTENT ============================ -->
			<div class="content">
			
			 
<div class="examples-nav"><a href="/examples/"><img src="/img/back_off.gif" alt="Back to List" /> <span class="back-to">Back To List</span></a>
</div>

<p class="ref-notice">
	This example is for Processing 3+. If you have a previous version, use the examples included with your software. If you see any errors or have suggestions, <a href="https://github.com/processing/processing-docs/issues?state=open">please let us know</a>.
</p>
<script src="../javascript/p5.min.js" type="text/javascript"></script>
<script src="../javascript/p5.dom.min.js" type="text/javascript"></script>


<div class="example">
<div id="p5container"></div><script type="text/javascript"></script>
<p class="doc"><strong>Spore 2</strong> by Mike Davis. <br />
<br />
 A short program for alife experiments. Click in the window to restart. Each cell is represented by a pixel on the display as well as an entry in the array 'cells'. Each cell has a run() method, which performs actions based on the cell's surroundings.  Cells run one at a time (to avoid conflicts like wanting to move to the same space) and in random order.</p>

<pre class="code">
 
World w;
int maxcells = 8000;
int numcells;
Cell[] cells = new Cell[maxcells];
color spore1, spore2, spore3, spore4;
color black = color(0, 0, 0);
// set lower for smoother animation, higher for faster simulation
int runs_per_loop = 10000;

void setup()
{
  size(640, 360);
  frameRate(24);
  reset();
}

void reset() {
  clearScreen();
  w = new World();
  spore1 = color(128, 172, 255);
  spore2 = color(64, 128, 255);
  spore3 = color(255, 128, 172);
  spore4 = color(255, 64, 128);
  numcells = 0;
  seed();
}

void seed()
{
  // Add cells at random places
  for (int i = 0; i &lt; maxcells; i++)
  {
    int cX = int(random(width));
    int cY = int(random(height));
    int c;
    float r = random(1);
    if (r &lt; 0.25) c = spore1;
    else if (r &lt; 0.5) c = spore2;
    else if (r &lt; 0.75) c = spore3;
    else c = spore4;
    if (w.getpix(cX, cY) == black)
    {
      w.setpix(cX, cY, c);
      cells[numcells] = new Cell(cX, cY);
      numcells++;
    }
  }
}

void draw() {
  // Run cells in random order
  for (int i = 0; i &lt; runs_per_loop; i++) {
    int selected = min((int)random(numcells), numcells - 1);
    cells[selected].run();
  }
}

void clearScreen() {
  background(0);
}

class Cell {
  int x, y;
  Cell(int xin, int yin) {
    x = xin;
    y = yin;
  }
  
  // Perform action based on surroundings
  void run() {
    // Fix cell coordinates
    while(x &lt; 0) {
      x+=width;
    }
    while(x &gt; width - 1) {
      x-=width;
    }
    while(y &lt; 0) {
      y+=height;
    }
    while(y &gt; height - 1) {
      y-=height;
    }

    // Cell instructions
    int myColor = w.getpix(x, y);
    if (myColor == spore1) {
      if (w.getpix(x - 1, y + 1) == black &amp;&amp; w.getpix(x + 1, y + 1) == black &amp;&amp; w.getpix(x, y + 1) == black) move(0, 1);
      else if (w.getpix(x - 1, y) == spore2 &amp;&amp; w.getpix(x - 1, y - 1) != black) move(0, -1);
      else if (w.getpix(x - 1, y) == spore2 &amp;&amp; w.getpix(x - 1, y - 1) == black) move(-1, -1);
      else if (w.getpix(x + 1, y) == spore1 &amp;&amp; w.getpix(x + 1, y - 1) != black) move(0, -1);
      else if (w.getpix(x + 1, y) == spore1 &amp;&amp; w.getpix(x + 1, y - 1) == black) move(1, -1);
      else move((int)random(3) - 1, 0);
    } else if (myColor == spore2) {
      if (w.getpix(x - 1, y + 1) == black &amp;&amp; w.getpix(x + 1, y + 1) == black &amp;&amp; w.getpix(x, y + 1) == black) move(0, 1);
      else if (w.getpix(x + 1, y) == spore1 &amp;&amp; w.getpix(x + 1, y - 1) != black) move(0, -1);
      else if (w.getpix(x + 1, y) == spore1 &amp;&amp; w.getpix(x + 1, y - 1) == black) move(1, -1);
      else if (w.getpix(x - 1, y) == spore2 &amp;&amp; w.getpix(x - 1, y - 1) != black) move(0, -1);
      else if (w.getpix(x - 1, y) == spore2 &amp;&amp; w.getpix(x - 1, y - 1) == black) move(-1, -1);
      else move((int)random(3) - 1, 0);
    }
    else if (myColor == spore3)
    {
      if (w.getpix(x - 1, y - 1) == black &amp;&amp; w.getpix(x + 1, y - 1) == black &amp;&amp; w.getpix(x, y - 1) == black) move(0, -1);
      else if (w.getpix(x - 1, y) == spore4 &amp;&amp; w.getpix(x - 1, y + 1) != black) move(0, 1);
      else if (w.getpix(x - 1, y) == spore4 &amp;&amp; w.getpix(x - 1, y + 1) == black) move(-1, 1);
      else if (w.getpix(x + 1, y) == spore3 &amp;&amp; w.getpix(x + 1, y + 1) != black) move(0, 1);
      else if (w.getpix(x + 1, y) == spore3 &amp;&amp; w.getpix(x + 1, y + 1) == black) move(1, 1);
      else move((int)random(3) - 1, 0);
    }
    else if (myColor == spore4)
    {
      if (w.getpix(x - 1, y - 1) == black &amp;&amp; w.getpix(x + 1, y - 1) == black &amp;&amp; w.getpix(x, y - 1) == black) move(0, -1);
      else if (w.getpix(x + 1, y) == spore3 &amp;&amp; w.getpix(x + 1, y + 1) != black) move(0, 1);
      else if (w.getpix(x + 1, y) == spore3 &amp;&amp; w.getpix(x + 1, y + 1) == black) move(1, 1);
      else if (w.getpix(x - 1, y) == spore4 &amp;&amp; w.getpix(x - 1, y + 1) != black) move(0, 1);
      else if (w.getpix(x - 1, y) == spore4 &amp;&amp; w.getpix(x - 1, y + 1) == black) move(-1, 1);
      else move((int)random(3) - 1, 0);
    }
  }
  
  // Will move the cell (dx, dy) units if that space is empty
  void move(int dx, int dy) {
    if (w.getpix(x + dx, y + dy) == black) {
      w.setpix(x + dx, y + dy, w.getpix(x, y));
      w.setpix(x, y, color(0));
      x += dx;
      y += dy;
    }
  }
}

//  The World class simply provides two functions, get and set, which access the
//  display in the same way as getPixel and setPixel.  The only difference is that
//  the World class's get and set do screen wraparound (&quot;toroidal coordinates&quot;).
class World {
  
  void setpix(int x, int y, int c) {
    while(x &lt; 0) x+=width;
    while(x &gt; width - 1) x-=width;
    while(y &lt; 0) y+=height;
    while(y &gt; height - 1) y-=height;
    set(x, y, c);
  }
  
  color getpix(int x, int y) {
    while(x &lt; 0) x+=width;
    while(x &gt; width - 1) x-=width;
    while(y &lt; 0) y+=height;
    while(y &gt; height - 1) y-=height;
    return get(x, y);
  }
}

void mousePressed() {
  reset();
}

</pre>


</div>


			
			</div>

			<!-- ==================================== FOOTER ============================ --> 
  			<div id="footer">
    			<div id="copyright">Processing was initiated by <a href="http://benfry.com/">Ben Fry</a> and <a href="http://reas.com">Casey Reas</a>. It is developed by a <a href="/people/">small team of volunteers</a>.</div> 
    			<div id="colophon">

                    <a href="/copyright.html">&copy; Info</a>

                </div>
  			</div>
  			
		</div>
		<script src="//ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js"></script>
		<script>window.jQuery || document.write('<script src="../javascript/jquery-1.9.1.min.js"><\/script>')</script>
		<script src="../javascript/site.js" type="text/javascript"></script>
		<script type="text/javascript">

		  var _gaq = _gaq || [];
		  _gaq.push(['_setAccount', 'UA-40016511-1']);
		  _gaq.push(['_setDomainName', 'processing.org']);
		  _gaq.push(['_trackPageview']);

		  (function() {
		    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
		    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
		    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
		  })();

		</script>
	</body>
</html>
